buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}


plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.7'
    //id "net.minecraftforge.gradle.forge" version "2.0.2"
}


repositories {

    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven { //JEI
        name 'DVS1 Maven FS'
        url "http://dvs1.progwml6.com/files/maven"
    }

    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'scala'
apply plugin: 'maven'

ext.configFile = file "build.properties"

String APIToken = ''
if (file('APIToken').exists()) {
    APIToken = new File('APIToken').text
}

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

String build = System.getenv("BUILD_NUMBER")

if (build == null || build == '') {
    version = "${config.minecraft_version}-${config.mod_version}"
} else {
    version = "${config.minecraft_version}-${config.mod_version}-B" + System.getenv("BUILD_NUMBER")
}
group= "com.teambr.bookshelf" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Bookshelf-API"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = config.minecraft_version + "-" + config.forge_version

    replace "@VERSION@", config.mod_version
    replaceIn "Reference.scala"

    runDir = "eclipse"

    mappings = "${config.MAPPINGS}"
    makeObfSourceJar = false
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }
    extension = 'jar'
    classifier = 'dev'
}

//task myJavadocs(type: Javadoc) {
//    source = sourceSets.main.allJava
//}

task javadocJar(type: Zip, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

/*task sourceJarScala(type: Jar, dependsOn: "sourceMainScala") {
    from 'build/sources/scala'
    classifier = 'sources'
}*/
task sourceJarScala(type: Jar, dependsOn: "classes") {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives devJar
    //archives javadocJar
    archives sourceJarScala
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.JEI_version}"
    compile "mcp.mobius.waila:Waila:${config.WAILA_version}:dev"
    compile "net.darkhax.tesla:Tesla:${config.TESLA_version}"
    deobfCompile "net.industrial-craft:industrialcraft-2:${config.IC2_version}:dev"
}

repositories {
    mavenCentral()
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { //JEI
        name 'DVS1 Maven FS'
        url "http://dvs1.progwml6.com/files/maven"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            //repository(url: System.properties['URL']) {
            repository(url: System.properties['URL']) {
                authentication(userName: System.properties['HTTPUSERNAME'], password: System.properties['HTTPPASSWORD'])
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName

                pom.project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'Bookshelf'
                    url 'https://github.com/TeamBR-Modding/Bookshelf'
                    scm {
                        url 'https://github.com/TeamBR-Modding/Bookshelf'
                        connection 'scm:git:git@github.com/TeamBR-Modding/Bookshelf.git'
                        developerConnection 'scm:git:git@github.com/TeamBR-Modding/Bookshelf.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/TeamBR-Modding/Bookshelf/issues'
                    }
                    licenses {
                        license {
                            name 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International License'
                            url 'http://creativecommons.org/licenses/by-nc-sa/4.0/'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Dyonovan'
                            name 'Dyonovan'
                            roles {
                                role 'developer'
                            }
                        }
                        developer {
                            id 'pauljoda'
                            name 'pauljoda'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}

curseforge  {
    apiKey = APIToken
    project {
        id = '232784'
        changelog = ''
        releaseType = 'release'
        addArtifact devJar
        addGameVersion '1.10.2'
    }
}

